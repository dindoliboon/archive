VERSION 2.00
Begin Form frmTab95 
   BackColor       =   &H8000000F&
   Caption         =   "Imitation Windows95 Style Tab Control"
   ClientHeight    =   3375
   ClientLeft      =   1005
   ClientTop       =   1485
   ClientWidth     =   6135
   Height          =   3780
   Left            =   945
   LinkTopic       =   "Form1"
   ScaleHeight     =   3375
   ScaleWidth      =   6135
   Top             =   1140
   Width           =   6255
   Begin PictureBox picTab 
      AutoRedraw      =   -1  'True
      BackColor       =   &H8000000F&
      FontBold        =   0   'False
      FontItalic      =   0   'False
      FontName        =   "MS Sans Serif"
      FontSize        =   8.25
      FontStrikethru  =   0   'False
      FontUnderline   =   0   'False
      Height          =   3135
      Index           =   0
      Left            =   120
      ScaleHeight     =   3105
      ScaleWidth      =   5865
      TabIndex        =   0
      Tag             =   "1"
      Top             =   120
      Width           =   5895
      Begin CommandButton cmdClose 
         BackColor       =   &H8000000F&
         Cancel          =   -1  'True
         Caption         =   "&Close"
         FontBold        =   0   'False
         FontItalic      =   0   'False
         FontName        =   "MS Sans Serif"
         FontSize        =   8.25
         FontStrikethru  =   0   'False
         FontUnderline   =   0   'False
         Height          =   375
         Left            =   4800
         TabIndex        =   20
         Top             =   2640
         Width           =   975
      End
      Begin CommandButton Command3 
         BackColor       =   &H8000000F&
         Caption         =   "Delete Current Button"
         FontBold        =   0   'False
         FontItalic      =   0   'False
         FontName        =   "MS Sans Serif"
         FontSize        =   8.25
         FontStrikethru  =   0   'False
         FontUnderline   =   0   'False
         Height          =   375
         Left            =   3960
         TabIndex        =   19
         Top             =   2160
         Width           =   1815
      End
      Begin CommandButton Command2 
         BackColor       =   &H8000000F&
         Caption         =   "Add Button 2"
         FontBold        =   0   'False
         FontItalic      =   0   'False
         FontName        =   "MS Sans Serif"
         FontSize        =   8.25
         FontStrikethru  =   0   'False
         FontUnderline   =   0   'False
         Height          =   375
         Left            =   4320
         TabIndex        =   18
         Top             =   1680
         Width           =   1455
      End
      Begin PictureBox picTab 
         BackColor       =   &H8000000F&
         Height          =   2055
         Index           =   4
         Left            =   480
         ScaleHeight     =   2025
         ScaleWidth      =   4305
         TabIndex        =   1
         Top             =   840
         Width           =   4335
         Begin Label Label10 
            BackColor       =   &H8000000F&
            Caption         =   "Feel free to use this code as is or modified in your own programs.  If you have any comments then drop me a note."
            FontBold        =   0   'False
            FontItalic      =   0   'False
            FontName        =   "MS Sans Serif"
            FontSize        =   8.25
            FontStrikethru  =   0   'False
            FontUnderline   =   0   'False
            Height          =   975
            Left            =   120
            TabIndex        =   13
            Top             =   600
            Width           =   4095
         End
         Begin Label Label2 
            BackColor       =   &H8000000F&
            Caption         =   "This was written by Phil Ricketts"
            FontBold        =   0   'False
            FontItalic      =   0   'False
            FontName        =   "MS Sans Serif"
            FontSize        =   8.25
            FontStrikethru  =   0   'False
            FontUnderline   =   0   'False
            Height          =   255
            Left            =   120
            TabIndex        =   6
            Top             =   120
            Width           =   4095
         End
         Begin Label Label1 
            BackColor       =   &H8000000F&
            Caption         =   "I can be reached at PRicketts@compuserve.com"
            FontBold        =   0   'False
            FontItalic      =   0   'False
            FontName        =   "MS Sans Serif"
            FontSize        =   8.25
            FontStrikethru  =   0   'False
            FontUnderline   =   0   'False
            Height          =   255
            Left            =   120
            TabIndex        =   5
            Top             =   360
            Width           =   4095
         End
      End
      Begin CommandButton Command1 
         BackColor       =   &H8000000F&
         Caption         =   "Enable4"
         FontBold        =   0   'False
         FontItalic      =   0   'False
         FontName        =   "MS Sans Serif"
         FontSize        =   8.25
         FontStrikethru  =   0   'False
         FontUnderline   =   0   'False
         Height          =   375
         Index           =   1
         Left            =   4800
         TabIndex        =   17
         Top             =   1200
         Width           =   975
      End
      Begin CommandButton Command1 
         BackColor       =   &H8000000F&
         Caption         =   "Disable4"
         FontBold        =   0   'False
         FontItalic      =   0   'False
         FontName        =   "MS Sans Serif"
         FontSize        =   8.25
         FontStrikethru  =   0   'False
         FontUnderline   =   0   'False
         Height          =   375
         Index           =   0
         Left            =   4800
         TabIndex        =   16
         Top             =   720
         Width           =   975
      End
      Begin PictureBox picTab 
         BackColor       =   &H8000000F&
         Height          =   2055
         Index           =   5
         Left            =   600
         ScaleHeight     =   2025
         ScaleWidth      =   4305
         TabIndex        =   14
         Top             =   960
         Width           =   4335
         Begin Label Label12 
            BackColor       =   &H8000000F&
            Caption         =   "Because I wanted to get the Windows95 feel into a VB3 program that I was working on and I didn't want to add any VBX's"
            FontBold        =   0   'False
            FontItalic      =   0   'False
            FontName        =   "MS Sans Serif"
            FontSize        =   8.25
            FontStrikethru  =   0   'False
            FontUnderline   =   0   'False
            Height          =   735
            Left            =   120
            TabIndex        =   15
            Top             =   120
            Width           =   4095
         End
      End
      Begin PictureBox picTab 
         BackColor       =   &H8000000F&
         Height          =   2055
         Index           =   3
         Left            =   360
         ScaleHeight     =   2025
         ScaleWidth      =   4305
         TabIndex        =   2
         Top             =   720
         Width           =   4335
         Begin Label Label5 
            BackColor       =   &H8000000F&
            Caption         =   "It can only cope with one row of Tabs"
            FontBold        =   0   'False
            FontItalic      =   0   'False
            FontName        =   "MS Sans Serif"
            FontSize        =   8.25
            FontStrikethru  =   0   'False
            FontUnderline   =   0   'False
            Height          =   255
            Left            =   120
            TabIndex        =   8
            Top             =   120
            Width           =   4095
         End
         Begin Label Label6 
            BackColor       =   &H8000000F&
            Caption         =   "Not exactly Windows95 look and feel, but it's pretty close"
            FontBold        =   0   'False
            FontItalic      =   0   'False
            FontName        =   "MS Sans Serif"
            FontSize        =   8.25
            FontStrikethru  =   0   'False
            FontUnderline   =   0   'False
            Height          =   495
            Left            =   120
            TabIndex        =   9
            Top             =   360
            Width           =   4095
         End
      End
      Begin PictureBox picTab 
         BackColor       =   &H8000000F&
         Height          =   2055
         Index           =   2
         Left            =   240
         ScaleHeight     =   2025
         ScaleWidth      =   4305
         TabIndex        =   3
         Top             =   600
         Width           =   4335
         Begin Label Label8 
            BackColor       =   &H8000000F&
            Caption         =   "Everything is done in VB, using Picture Box controls and the Line and Print methods."
            FontBold        =   0   'False
            FontItalic      =   0   'False
            FontName        =   "MS Sans Serif"
            FontSize        =   8.25
            FontStrikethru  =   0   'False
            FontUnderline   =   0   'False
            Height          =   375
            Left            =   120
            TabIndex        =   11
            Top             =   600
            Width           =   4095
         End
         Begin Label Label7 
            BackColor       =   &H8000000F&
            Caption         =   "No VBX's or DLL's.  Only one API call - and that is just to get the exact button hilight colour."
            FontBold        =   0   'False
            FontItalic      =   0   'False
            FontName        =   "MS Sans Serif"
            FontSize        =   8.25
            FontStrikethru  =   0   'False
            FontUnderline   =   0   'False
            Height          =   495
            Left            =   120
            TabIndex        =   10
            Top             =   120
            Width           =   4095
         End
         Begin Label Label9 
            BackColor       =   &H8000000F&
            Caption         =   "Just modify the CreateTab() routine to set the number of tabs and the text to appear on each."
            FontBold        =   0   'False
            FontItalic      =   0   'False
            FontName        =   "MS Sans Serif"
            FontSize        =   8.25
            FontStrikethru  =   0   'False
            FontUnderline   =   0   'False
            Height          =   855
            Left            =   120
            TabIndex        =   12
            Top             =   1080
            Width           =   4095
         End
      End
      Begin PictureBox picTab 
         BackColor       =   &H8000000F&
         Height          =   2055
         Index           =   1
         Left            =   120
         ScaleHeight     =   2025
         ScaleWidth      =   4305
         TabIndex        =   4
         Top             =   480
         Width           =   4335
         Begin Label Label3 
            BackColor       =   &H8000000F&
            Caption         =   "Click on one of the Tabs for more information."
            FontBold        =   0   'False
            FontItalic      =   0   'False
            FontName        =   "MS Sans Serif"
            FontSize        =   8.25
            FontStrikethru  =   0   'False
            FontUnderline   =   0   'False
            Height          =   255
            Left            =   120
            TabIndex        =   7
            Top             =   120
            Width           =   4095
         End
      End
   End
End
Option Explicit

' Written by Phil Ricketts - EMail PRicketts@compuserve.com [100042,2041]
' =======================================================================

' Originally - Feb '96
' This version - Nov '96
' tidied up for public display - May '97

' a Tab Class implemented in VB3
' there can only be one Tab on a form
' use ...
' -------
' Call InitialiseTab(pic)
' Call AddTab("Caption", picTab())
' Call EnableTab(iIndex, False)
' Call DeleteTab(iIndex)
' Call InsertTab("Caption", picTab(), iIndex)
' Call PaintTab(iIndex, iClickX, iClickY)
' Call SelectTab(iIndex)
' TabChange(iIndex) event is triggered when the Tab changes

Dim fiNumTabs As Integer
Dim foTabControl As PictureBox
Dim fsTabCaption() As String
Dim fiTabEnabled() As Integer
Dim foTabContainer() As PictureBox
Dim fiSelectedTab As Integer

Dim vbButtonHilight As Long
Dim vbButtonShadow As Long
Dim vbButtonFace As Long
Dim vbButtonText As Long

Declare Function GetSysColor Lib "user" (ByVal nIndex As Integer) As Long

Sub AddTab (ByVal sTabText As String, picContainer As PictureBox)

    ' adds a tab to the array that holds the tabs

    fiNumTabs = fiNumTabs + 1
    ReDim Preserve fsTabCaption(fiNumTabs)
    ReDim Preserve fiTabEnabled(fiNumTabs)
    ReDim Preserve foTabContainer(fiNumTabs)
    fsTabCaption(fiNumTabs) = sTabText
    fiTabEnabled(fiNumTabs) = True
    Set foTabContainer(fiNumTabs) = picContainer

    Call PaintTab(fiSelectedTab, 0, 0)

End Sub

Sub cmdClose_Click ()

    Unload Me

End Sub

Sub Command1_Click (Index As Integer)

    Select Case Index
    Case 0:
        Call EnableTab(4, False)
    Case 1:
        Call EnableTab(4, True)
    End Select

End Sub

Sub Command2_Click ()

    Call InsertTab("Z", picTab(1), 2)

End Sub

Sub Command3_Click ()

    Call DeleteTab(fiSelectedTab)

End Sub

Sub CreateTab (pic As PictureBox)

    ' application specific routine - this should be modified to suit your
    ' requirements, and should be called very early on in the Form_Load event

    Call InitialiseTab(pic)
    Call AddTab("Top Tab", picTab(1))
    Call AddTab("How does it work ?", picTab(2))
    Call AddTab("Limitations", picTab(3))
    Call AddTab("Who did this ?", picTab(4))
    Call AddTab("Why ?", picTab(5))

End Sub

Sub DeleteTab (ByVal iPosition As Integer)

    ' removes a tab

    Dim iCount As Integer

    If iPosition > fiNumTabs Then
        ' ignore it
    Else
        
        For iCount = iPosition + 1 To fiNumTabs
            fsTabCaption(iCount - 1) = fsTabCaption(iCount)
            fiTabEnabled(iCount - 1) = fiTabEnabled(iCount)
            Set foTabContainer(iCount - 1) = foTabContainer(iCount)
        Next
    
        fiNumTabs = fiNumTabs - 1
        ReDim Preserve fsTabCaption(fiNumTabs)
        ReDim Preserve fiTabEnabled(fiNumTabs)
        ReDim Preserve foTabContainer(fiNumTabs)
        
        If fiSelectedTab >= fiNumTabs Then
            fiSelectedTab = fiNumTabs
        End If
        
        Call PaintTab(fiSelectedTab, 0, 0)
    End If

End Sub

Sub EnableTab (ByVal iTab As Integer, ByVal iEnabled As Integer)

    ' enables or disables a tab

    If iTab <= fiNumTabs Then
        fiTabEnabled(iTab) = iEnabled
        Call PaintTab(fiSelectedTab, 0, 0)
    End If

End Sub

Sub Form_Load ()

    Call CreateTab(picTab(0))

    ' when the form is being loaded for the first time it is necessary
    ' to force the PictureBox control to draw itself,
    ' NOTE: this could be achieved a number of different ways, if the
    ' PictureBox control has AutoRedraw set to True then the Paint Event
    ' will not be fired when the form is loading, and it will have to
    ' be done this way, if AutoRedraw is set to False then the Paint
    ' Event will be fired and the PaintTab routine could be called
    ' from there, however you will have to set the Tag property to the
    ' number of the Tab which you want to draw - in this case 1
    Call PaintTab(1, 0, 0)

    ' setup and position the containers - when a tab is selected from the
    ' imitation tab control a specific group of controls is made visible
    ' I think the easiest way to control this is to place all of the controls
    ' for each tab into its own container control, I am using PictureBox
    ' controls, but you could use any container control such as a 3D panel
    ' I leave the BorderStyle set to 1 so that they are easy to find, I also
    ' have them all offset at design time so that I can select them easily,
    ' NOTE: picContainer(0) is properly positioned at design time
    picTab(2).Move picTab(1).Left, picTab(1).Top
    picTab(3).Move picTab(1).Left, picTab(1).Top
    picTab(4).Move picTab(1).Left, picTab(1).Top
    picTab(5).Move picTab(1).Left, picTab(1).Top
    picTab(1).BorderStyle = 0
    picTab(2).BorderStyle = 0
    picTab(3).BorderStyle = 0
    picTab(4).BorderStyle = 0
    picTab(5).BorderStyle = 0

End Sub

Sub InitialiseTab (pic As PictureBox)

    ' essential that this is called before any other routines dealing with the tab

    fiNumTabs = 0
    Set foTabControl = pic
    ReDim fsTabCaption(0)
    ReDim fiTabEnabled(0)
    ReDim foTabContainer(0)

    vbButtonHilight = GetSysColor(20)
    vbButtonFace = GetSysColor(15)
    vbButtonShadow = GetSysColor(16)
    vbButtonText = GetSysColor(18)

End Sub

Sub InsertTab (ByVal sTabText As String, picContainer As PictureBox, ByVal iPosition As Integer)

    ' adds a tab to a specific position in the array of tabs

    Dim iCount As Integer

    If iPosition > fiNumTabs Then
        ' just add it on to the end
        Call AddTab(sTabText, picContainer)
    Else
        fiNumTabs = fiNumTabs + 1
        ReDim Preserve fsTabCaption(fiNumTabs)
        ReDim Preserve fiTabEnabled(fiNumTabs)
        ReDim Preserve foTabContainer(fiNumTabs)
        
        For iCount = fiNumTabs To iPosition + 1 Step -1
            fsTabCaption(iCount) = fsTabCaption(iCount - 1)
            fiTabEnabled(iCount) = fiTabEnabled(iCount - 1)
            Set foTabContainer(iCount) = foTabContainer(iCount - 1)
        Next
        fsTabCaption(iPosition) = sTabText
        fiTabEnabled(iPosition) = True
        Set foTabContainer(iPosition) = picContainer
    
        If fiSelectedTab >= iPosition Then
            fiSelectedTab = fiSelectedTab + 1
        End If
        Call PaintTab(fiSelectedTab, 0, 0)
    End If

End Sub

Sub PaintTab (ByVal iForcedTab%, ByVal X!, ByVal Y!)

    ' this routine can be called in two different ways, it can either be
    ' called with the iForcedTab% parameter set to the number of the tab
    ' which is to be selected - in which case the X! and Y! parameters will
    ' be ignored, or it can be called with the iForcedTab% parameter set to
    ' 0 - in which case the X! and Y! parameters will be used to calculate
    ' which tab, if any, has been selected, the former case is so that we can
    ' programatically select a tab, and so that we can handle the redrawing
    ' if necessary, the latter case is so that this can be called when a
    ' MouseDown event occurs on the PictureBox control

    ' the colours used for text and line drawing
    Const BLACKCOLOUR = &H0

    ' - this could be changed to the correct buttonhighlight colour by calling GetSystemColor

    ' sizes and offsets
    Dim iLineWidth As Integer
    Dim iTabHeight As Integer
    Dim iTextBorder As Integer
    Dim iTabExtra As Integer
    Dim iTabLeftOffset As Integer
    Dim iTabGap As Integer

    Dim iCount As Integer
    Dim iTab As Integer
    Dim iTabLeft As Integer
    Dim iTabWidth As Integer
    Dim iTabRight As Integer
    Dim iTabTop As Integer
    Dim iTabBottom As Integer
    Dim iSelectedTab As Integer

    ' set up the size variables
    ' NOTE: By default I leave the ScaleMode set to Twips
    ' this routine will have to be modified to cope with other scale modes
    iLineWidth% = Screen.TwipsPerPixelX
    iTabHeight% = 22 * iLineWidth%
    iTextBorder% = 8 * iLineWidth%
    iTabExtra% = 3 * iLineWidth%
    iTabLeftOffset% = 3 * iLineWidth%
    iTabGap% = 0 * iLineWidth%
    ' changing any of these will change the appearance of the Tab

    ' work out which tab has been selected
    If iForcedTab% = 0 Or iForcedTab% > fiNumTabs Then
        ' is this a click in one of the tabs
        ' if not then bomb out
        If Y! > iTabHeight% Or Y! < iTabExtra% Then
            ' a click in the body of the tab - ignore it
            Exit Sub
        End If
    
        iTabRight% = iTabLeftOffset%
        iSelectedTab% = 0
        For iCount% = 1 To fiNumTabs
            iTabRight% = iTabRight% + foTabControl.TextWidth(fsTabCaption(iCount%)) + (iTextBorder% * 2) + iTabGap%
            If X! < iTabRight% Then
                iSelectedTab% = iCount%
                Exit For
            End If
        Next
        If iSelectedTab% = 0 Then
            ' a click in the tab area but after the last tab
            Exit Sub
        End If
        If fiTabEnabled(iSelectedTab) = False Then
            Exit Sub
        End If
    Else
        iSelectedTab% = iForcedTab%
    End If
    foTabControl.Tag = CStr(iSelectedTab%)
    
    ' set Border Style to 0,
    ' I leave it at 1 so that I can see it at design time
    If foTabControl.BorderStyle <> 0 Then
        ' this triggers a refresh - so only do it if necessary
        foTabControl.BorderStyle = 0
    End If
    If foTabControl.FontBold <> False Then
        ' the default for a PictureBox control in VB3 is for FontBold to
        ' be True, Windows95 uses non-bold
        foTabControl.FontBold = False
    End If
    If foTabControl.BackColor <> vbButtonFace Then
        ' the default is for this to be the default window colour
        foTabControl.BackColor = vbButtonFace
    End If

    ' erase the old Tab - by drawing a grey rectangle over the entire area of it
    foTabControl.Line (0, 0)-(foTabControl.Width, foTabControl.Height), vbButtonFace, BF

    ' draw the tabs - left, right, top, text
    ' draw them in reverse order - incase they overlap
    iTabTop% = iTabExtra%
    For iCount% = fiNumTabs To 1 Step -1
        ' find the left co-ord by working out the width of all previous tabs
        If iCount% <> iSelectedTab% Then
            iTabLeft% = iTabLeftOffset%
            If iCount% > 1 Then
                For iTab% = 1 To iCount% - 1
                    iTabWidth% = foTabControl.TextWidth(fsTabCaption(iTab%)) + (iTextBorder% * 2) + iTabGap%
                    iTabLeft% = iTabLeft% + iTabWidth%
                Next
            End If
            iTabWidth% = foTabControl.TextWidth(fsTabCaption(iCount%)) + (iTextBorder% * 2)
            iTabRight% = iTabLeft% + iTabWidth%
            foTabControl.Line (iTabLeft% + iLineWidth%, iTabHeight%)-(iTabLeft% + iLineWidth%, iTabExtra%), vbButtonHilight
            foTabControl.Line (iTabLeft% + (iLineWidth% * 2), iTabExtra%)-(iTabRight% - iLineWidth%, iTabExtra%), vbButtonHilight
            foTabControl.Line (iTabRight% - iLineWidth%, iTabHeight%)-(iTabRight% - iLineWidth%, iTabExtra%), vbButtonShadow
            foTabControl.Line (iTabRight%, iTabHeight%)-(iTabRight%, iTabExtra% + iLineWidth%), BLACKCOLOUR
            ' draw the text
            If fiTabEnabled(iCount) = True Then
                foTabControl.CurrentX = iTabLeft% + ((iTabWidth% - foTabControl.TextWidth(fsTabCaption(iCount%))) / 2)
                foTabControl.CurrentY = ((iTabExtra% - iLineWidth% + (iTabHeight% - foTabControl.TextHeight(fsTabCaption(iCount%))) / 2))
                foTabControl.ForeColor = vbButtonText
                foTabControl.Print fsTabCaption(iCount%)
            Else
                ' draw the text disabled
                foTabControl.CurrentX = iTabLeft% + ((iTabWidth% - foTabControl.TextWidth(fsTabCaption(iCount%))) / 2) + iLineWidth
                foTabControl.CurrentY = ((iTabExtra% - iLineWidth% + (iTabHeight% - foTabControl.TextHeight(fsTabCaption(iCount%))) / 2)) + iLineWidth
                foTabControl.ForeColor = vbButtonHilight
                foTabControl.Print fsTabCaption(iCount%)
                foTabControl.CurrentX = iTabLeft% + ((iTabWidth% - foTabControl.TextWidth(fsTabCaption(iCount%))) / 2)
                foTabControl.CurrentY = ((iTabExtra% - iLineWidth% + (iTabHeight% - foTabControl.TextHeight(fsTabCaption(iCount%))) / 2))
                foTabControl.ForeColor = vbButtonShadow
                foTabControl.Print fsTabCaption(iCount%)
            End If
        Else
            ' this is the selected tab - dont bother drawing it - we'll do that last
        End If
    Next
    
    ' draw the border - top, left, bottom, right
    foTabControl.Line (iLineWidth%, iTabHeight%)-(foTabControl.Width, iTabHeight%), vbButtonHilight
    foTabControl.Line (0, iTabHeight%)-(0, foTabControl.Height), vbButtonHilight
    foTabControl.Line (0, foTabControl.Height - iLineWidth%)-(foTabControl.Width, foTabControl.Height - iLineWidth%), BLACKCOLOUR
    foTabControl.Line (foTabControl.Width - iLineWidth%, iTabHeight%)-(foTabControl.Width - iLineWidth%, foTabControl.Height - iLineWidth%), BLACKCOLOUR
    foTabControl.Line (iLineWidth%, foTabControl.Height - (iLineWidth% * 2))-(foTabControl.Width - iLineWidth%, foTabControl.Height - (iLineWidth% * 2)), vbButtonShadow
    foTabControl.Line (foTabControl.Width - (iLineWidth% * 2), iTabHeight% + iLineWidth%)-(foTabControl.Width - (iLineWidth% * 2), foTabControl.Height - (iLineWidth% * 2)), vbButtonShadow
                              
    ' find the left co-ord by working out the width of all previous tabs
    iTabLeft% = iTabLeftOffset%
    If iSelectedTab% > 1 Then
        For iTab% = 1 To iSelectedTab% - 1
            iTabWidth% = foTabControl.TextWidth(fsTabCaption(iTab%)) + (iTextBorder% * 2) + iTabGap%
            iTabLeft% = iTabLeft% + iTabWidth%
        Next
    End If
    iTabWidth% = foTabControl.TextWidth(fsTabCaption(iSelectedTab%)) + (iTextBorder% * 2)
    iTabRight% = iTabLeft% + iTabWidth%
    
    ' adjust the left and right positions by the extra width of the selected tab
    iTabLeft% = iTabLeft% - iTabExtra%
    iTabRight% = iTabRight% + iTabExtra%
    
    ' draw the selected tab - bottom, left, right, top, text
    ' erase the area first
    foTabControl.Line (iTabLeft% + iLineWidth%, iTabTop%)-(iTabRight% - iLineWidth, iTabHeight% + iLineWidth%), vbButtonFace, BF
    foTabControl.Line (iTabLeft%, iTabHeight%)-(iTabLeft%, iLineWidth%), vbButtonHilight
    foTabControl.Line (iTabLeft% + iLineWidth%, iLineWidth%)-(iTabRight% - iLineWidth%, iLineWidth%), vbButtonHilight
    foTabControl.Line (iTabRight%, iTabHeight% - iLineWidth%)-(iTabRight%, (iLineWidth% * 2)), BLACKCOLOUR
    foTabControl.Line (iTabRight% - iLineWidth%, iTabHeight%)-(iTabRight% - iLineWidth%, iLineWidth%), vbButtonShadow
    ' draw the text
    If fiTabEnabled(iSelectedTab%) = True Then
        foTabControl.CurrentX = iTabLeft% + ((iTabWidth% + (iTabExtra% * 2) - foTabControl.TextWidth(fsTabCaption(iSelectedTab%))) / 2)
        foTabControl.CurrentY = ((iTabHeight% - foTabControl.TextHeight(fsTabCaption(iSelectedTab%))) / 2)
        foTabControl.ForeColor = vbButtonText
        foTabControl.Print fsTabCaption(iSelectedTab%)
    Else
        ' draw the text disabled
        foTabControl.CurrentX = iTabLeft% + ((iTabWidth% + (iTabExtra% * 2) - foTabControl.TextWidth(fsTabCaption(iSelectedTab%))) / 2) + iLineWidth
        foTabControl.CurrentY = ((iTabHeight% - foTabControl.TextHeight(fsTabCaption(iSelectedTab%))) / 2) + iLineWidth
        foTabControl.ForeColor = vbButtonHilight
        foTabControl.Print fsTabCaption(iSelectedTab%)
        foTabControl.CurrentX = iTabLeft% + ((iTabWidth% + (iTabExtra% * 2) - foTabControl.TextWidth(fsTabCaption(iSelectedTab%))) / 2)
        foTabControl.CurrentY = ((iTabHeight% - foTabControl.TextHeight(fsTabCaption(iSelectedTab%))) / 2)
        foTabControl.ForeColor = vbButtonShadow
        foTabControl.Print fsTabCaption(iSelectedTab%)
    End If

    ' selected tab specific code
    ' trigger code here to hide and show controls
    ' to imitate a new tab being shown
    For iCount = 1 To fiNumTabs
        If iCount <> iSelectedTab Then
            foTabContainer(iCount).Visible = False
        End If
    Next
    foTabContainer(iSelectedTab).Visible = True

    If fiSelectedTab <> iSelectedTab Then
        Call TabChange(iSelectedTab, fiSelectedTab)
        fiSelectedTab = iSelectedTab
    End If

End Sub

Sub picTab_MouseDown (Index As Integer, Button As Integer, Shift As Integer, X As Single, Y As Single)

    ' there is a click somewhere on the PictureBox control
    ' this code is in the mouse down event because we need the
    ' co-ordinates of the click so that we can work out whether
    ' the click is over one of the tabs, calling PaintTab with
    ' the second parameter = 0 tells it to use the third and
    ' fourth parameters to work out which tab to display
    If Index = 0 Then
        Call PaintTab(0, X!, Y!)
    End If

End Sub

Sub SelectTab (ByVal iTab As Integer)

    Call PaintTab(iTab, 0, 0)

End Sub

Sub TabChange (ByVal iNewIndex As Integer, ByVal iPrevIndex As Integer)

    ' this code is triggered when a new tab is selected
    If iPrevIndex > 0 Then
        'MsgBox "Tab : " & iNewIndex & " was : " & iPrevIndex
    End If

End Sub

